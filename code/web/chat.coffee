цепь = require './conveyor'
sanitize = require('validator').sanitize

websocket = global.websocket
хранилище = global.db
приложение = global.application
http = global.application_tools.http

болталка = websocket
	.of('/болталка')
	.on 'connection', (соединение) ->
		соединение.on 'пользователь', (id) ->
			цепь()
				.сделать ->
					хранилище.collection('people').findOne({ '_id': id }, @)
				.сделать (пользователь) ->
					if not пользователь?
						console.error 'Пользователь с номером ' + id + ' не найден'
						соединение.emit 'ошибка', ошибка: 'Пользователь с номером ' + id + ' не найден'
					соединение.set 'пользователь', пользователь, @
				.ошибка (ошибка) ->
					console.error ошибка
					соединение.emit 'ошибка', ошибка: ошибка
				.сделать ->
					соединение.emit 'готов'
		
		соединение.on 'сообщение', (сообщение) ->
			сообщение = sanitize(сообщение).xss()
			время = new Date()
			
			цепь()
				.сделать ->
					соединение.get 'пользователь', @.в 'отправитель'
				.сделать (отправитель) ->
					хранилище.collection('chat').save { 'отправитель': отправитель._id, 'сообщение': сообщение, 'время': время }, @
				.сделать () ->
					соединение.broadcast.emit('сообщение', сообщение: { отправитель: @.переменная('отправитель').имя, сообщение: сообщение, время: время })
				.ошибка (ошибка) ->
					console.error ошибка
					соединение.emit 'ошибка', ошибка: ошибка
	
		###
		соединение.emit('a message',
		{
			that: 'only',
			'/chat': 'will get'
		})
		
		болталка.emit('a message',
		{
			everyone: 'in',
			'/chat': 'will get'
		})
		###
		
http.get '/болталка/сообщения', (ввод, вывод) ->
	цепочка = цепь()
	цепочка
		.сделать ->
			хранилище.collection('chat').find().toArray(@.в 'сообщения')
		.все_вместе (сообщение) ->
			хранилище.collection('people').findOne { _id: сообщение.отправитель }, @
		.сделать (отправители) ->
			@.переменная('сообщения').forEach (сообщение, индекс) ->
				сообщение.отправитель =
					имя: отправители[индекс].имя
					'адресное имя': отправители[индекс]['адресное имя']
			@()
		.сделать ->
			вывод.send сообщения: @.переменная('сообщения')
		.ошибка (ошибка) ->
			console.error ошибка
			вывод.send ошибка: ошибка