хранилище = global.db
http = global.application_tools.http
Цепочка = require './../conveyor'
цепь = require './../web_conveyor'

снасти = require './../tools'

http.get '/заметка', (ввод, вывод) ->
	снасти.получить_данные_человека ввод.настройки.address_name, вывод, (данные) ->
		вывод.send данные: данные
		
получить_данные_раздела = (путь, вывод, возврат) ->
	new Цепочка()
		.сделать ->
			if путь?
				хранилище.collection('library_categories').findOne { путь: путь }, @.в 'раздел'
			else
				(@.в 'раздел')(null, {})

		.сделать (раздел) ->
			if not раздел?
				return вывод.send
					ошибка:
						текст: "Раздел читальни или заметка «#{путь}» не найдены"
						уровень: 'ничего страшного'
				
			раздел.подразделы = []
			раздел.заметки = []
			@ null, раздел

		.сделать (раздел) ->
			if раздел._id?
				хранилище.collection('library_categories').find(надраздел: раздел._id).toArray @
			else
				хранилище.collection('library_categories').find(надраздел: { '$exists': 0 }).toArray @
		
		.каждый (подраздел) ->
			раздел = @.переменная 'раздел'
			раздел.подразделы.push подраздел
			@()

		.сделать (раздел) ->
			раздел = @.переменная 'раздел'
		
			if раздел._id?
				хранилище.collection('library_articles').find(раздел: раздел._id).toArray @
			else
				хранилище.collection('library_articles').find(раздел: { '$exists': 0 }).toArray @
		
		.каждый (заметка) ->
			раздел = @.переменная 'раздел'
			раздел.заметки.push заметка
			@()

		.сделать (раздел) ->
			раздел = @.переменная 'раздел'
			возврат null, раздел
			
		.ошибка (ошибка) ->
			возврат ошибка

http.get '/раздел читальни', (ввод, вывод) ->
	путь = ввод.настройки.путь
	
	цепь(вывод)
		.сделать ->
			получить_данные_раздела(путь, вывод, @)
			
		.сделать (данные) ->
			вывод.send данные

http.get "/раздел или заметка?", (ввод, вывод) ->
	цепь(вывод)
		.сделать ->
			хранилище.collection('library_articles').findOne({ путь: ввод.настройки.путь }, @)
			
		.сделать (заметка) ->
			if заметка?
				return вывод.send(заметка: заметка)
			else
				return вывод.send(раздел: yes)