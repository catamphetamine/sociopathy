хранилище = global.db
http = global.application_tools.http
цепь = require './../web_conveyor'

хранилище.bind 'people',
	выбрать: (настройки, возврат) ->
		условия = настройки.условия || {}
		@find(условия, { skip: настройки.с - 1, limit: настройки.сколько }).toArray возврат

http.get '/люди', (ввод, вывод) ->
	цепь(вывод)
		.делать ->
			хранилище.collection('people').выбрать { с: ввод.настройки.с, сколько: ввод.настройки.сколько }, @

		.делать ->
			хранилище.collection('people').count @
		
		.сделать (люди, поголовье) ->
			есть_ли_ещё = поголовье > (ввод.настройки.с - 1 + ввод.настройки.сколько)
			вывод.send люди: люди, 'есть ещё?': есть_ли_ещё 

http.get "/страница/люди/:address_name", (ввод, вывод) ->
	address_name = ввод.params.address_name
	
	цепь(вывод)
		.сделать ->
			хранилище.collection('people').findOne { 'адресное имя': address_name }, @

		.сделать (пользователь) ->
			данные = {}
			
			if not пользователь?
				данные.ошибка = "Пользователь «#{address_name}» не состоит в нашей сети"
				console.error данные.ошибка
			else
				данные.данные_пользователя =
					имя: пользователь.имя
					описание: пользователь.описание
					картинка: пользователь.картинка
					пол: пользователь.пол
					откуда: пользователь.откуда
					
				данные.данные_пользователя = JSON.stringify данные.данные_пользователя
				
			снасти.отдать_страницу 'человек', данные, ввод, вывод

http.get '/человек', (ввод, вывод) ->
	снасти.получить_данные_человека ввод.настройки.address_name, вывод, (данные) ->
		вывод.send данные: данные
		